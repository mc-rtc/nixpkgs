{}:

final: prev:
let
  withCCache = packages: with builtins; listToAttrs (map
    (name: {
      inherit name; value = (getAttr name prev).override { stdenv = prev.ccacheStdenv; };
    })
    packages);
in
{
  ccacheWrapper = prev.ccacheWrapper.override {
    extraConfig = ''
      export CCACHE_COMPRESS=1
      export CCACHE_DIR="/var/cache/ccache"
      export CCACHE_UMASK=007
      export CCACHE_SLOPPINESS=random_seed
      if [ ! -d "$CCACHE_DIR" ]; then
        echo "====="
        echo "Directory '$CCACHE_DIR' does not exist"
        echo "Please create it with:"
        echo "  sudo mkdir -m0770 '$CCACHE_DIR'"
        echo "  sudo chown root:nixbld '$CCACHE_DIR'"
        echo "====="
        exit 1
      fi
      if [ ! -w "$CCACHE_DIR" ]; then
        echo "====="
        echo "Directory '$CCACHE_DIR' is not accessible for user $(whoami)"
        echo "Please verify its access permissions"
        echo "====="
        exit 1
      fi
    '';
  };
} // withCCache [
  "spacevecalg"
  "rbdyn"
  "sch-core"
  "eigen-qld"
  "eigen-quadprog"
  "tasks"
  "tvm"
  "mc-rtc-magnum"
  "mc-rtc-magnum-standalone"
  # "mc-rtc-rviz-panel" # how to make it work for buildRosPackage?
  # "mc-rtc"
]
